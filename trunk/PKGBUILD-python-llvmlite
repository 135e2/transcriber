# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=llvmlite
pkgbase="python-${_pkgname}"
pkgname=("${pkgbase}")
pkgver=0.40.0rc1
_commit=243cc57a14469857e65fa605937a48dfd24ab471  # refs/tags/v0.40.0rc1
pkgrel=1
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('x86_64')
depends=('gcc-libs' 'glibc' 'python' 'llvm14-libs')
makedepends=('cmake' 'llvm14' 'git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
checkdepends=('python-pytest')
url="https://github.com/numba/${_pkgname}"
license=('BSD')

source=("${_pkgname}::git+${url}#tag=$_commit")
sha256sums=('SKIP')

prepare() {
    cd "${_pkgname}"
    sed -e 's/3.11/3.12/' -i setup.py
}

build() {
    cd "${_pkgname}"
    LLVM_CONFIG=llvm-config-14 python -m build --wheel --no-isolation
}

check() {
    cd "${_pkgname}"
    pytest -vv $_pkgname/tests
}

package() {
    cd "${_pkgname}"

    python -m installer --destdir="$pkgdir" dist/*.whl

    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
